apply plugin: 'com.android.application'

def getVersionCode = {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-list', '--all', '--count'
        standardOutput = stdout
    }
    return Integer.parseInt(stdout.toString().trim())
}

def getVersionName = {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "nl.amstelveencollege.rooster"
        minSdkVersion 15
        versionCode getVersionCode()
        versionName getVersionName()
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
//        debug {
//            minifyEnabled true
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
//        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'ch.acra:acra:4.7.0-RC.3'
    compile 'com.wdullaer:materialdatetimepicker:2.0.2'
    compile 'com.github.afollestad.material-dialogs:core:0.8.5.2'
    compile 'com.google.code.gson:gson:2.5'
//    compile 'ch.tutti.android.bottomsheet:library:1.0'
    compile project(':framework')
    compile project(':bottomsheet')
}
